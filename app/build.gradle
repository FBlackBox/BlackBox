plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "top.niunaijun.blackboxa"
        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 12
        versionName "2.1.0"

        flavorDimensions "BlackBox32"
    }

    productFlavors {
        BlackBox32 {
            applicationId "top.niunaijun.blackboxa32"
            ndk {
                abiFilters "armeabi-v7a"
            }
            resValue "string", "app_name", "BlackBox32"
            manifestPlaceholders = [APP_ICON      : "@mipmap/ic_launcher",
                                    APP_ROUND_ICON: "@mipmap/ic_launcher_round"]
        }

        BlackBox64 {
            applicationId "top.niunaijun.blackboxa64"
            ndk {
                abiFilters "arm64-v8a"
            }
            resValue "string", "app_name", "BlackBox64"
            manifestPlaceholders = [APP_ICON      : "@mipmap/ic_launcher",
                                    APP_ROUND_ICON: "@mipmap/ic_launcher_round"]
        }

        BlackBox32Beta {
            applicationId "top.niunaijun.blackboxa32_beta"
            ndk {
                abiFilters "armeabi-v7a"
            }
            resValue "string", "app_name", "BlackBox32 Beta"
            manifestPlaceholders = [APP_ICON      : "@mipmap/ic_launcher_beta",
                                    APP_ROUND_ICON: "@mipmap/ic_launcher_round_beta"]
        }

        BlackBox64Beta {
            applicationId "top.niunaijun.blackboxa64_beta"
            ndk {
                abiFilters "arm64-v8a"
            }
            resValue "string", "app_name", "BlackBox64 Beta"
            manifestPlaceholders = [APP_ICON      : "@mipmap/ic_launcher_round_beta",
                                    APP_ROUND_ICON: "@mipmap/ic_launcher_round_beta"]
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    dexOptions {
        preDexLibraries false
        maxProcessCount 8
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        warningsAsErrors false
        disable "UnusedResources", 'RestrictedApi'
        textOutput "stdout"
        textReport false
        check 'NewApi', 'InlinedApi'
    }
}


tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}


repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])
    implementation project(':Bcore')

    //android
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.3.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.recyclerview:recyclerview:1.2.0"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation "androidx.activity:activity-ktx:1.2.2"
    implementation "androidx.fragment:fragment-ktx:1.3.3"
    implementation "androidx.preference:preference-ktx:1.1.1"

    //coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"

    //viewModel liveData  lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"


    //third

    implementation 'com.tbuonomo:dotsindicator:4.2'
    //viewPager2 指示点
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    //dialog
    implementation 'com.github.nukc.stateview:kotlin:2.2.0'
    //状态控制控件
    implementation 'com.roger.catloadinglibrary:catloadinglibrary:1.0.9'
    //加载dialog
    implementation 'com.github.Ferfalk:SimpleSearchView:0.2.0'
    //searchView
    implementation 'com.github.Othershe:CornerLabelView:1.0.0'
    //图标角标

    // osmdroid
    implementation 'org.osmdroid:osmdroid-android:6.1.11'
    //adapter
    implementation 'com.gitee.cbfg5210:RVAdapter:0.3.7'

    //悬浮窗
    implementation 'com.imuxuan:floatingview:1.6'
}
